include "utils.nv"

type attribute = option[int]

let nodes = 4

let edges = {
 0=1;
 0=2;
 1=2;
 1=3;
 2=3;
}

symbolic fail01 : bool
symbolic fail02 : bool
symbolic fail12 : bool
symbolic fail13 : bool
symbolic fail23 : bool

let bool2int b = if b then 1 else 0

require
  let k = (bool2int fail01) +
          (bool2int fail02) +
	  (bool2int fail12) +
	  (bool2int fail13) +
	  (bool2int fail23)
  in
  k <= 2

let merge node x y =
  mapbetter (fun x y -> if x < y then x else y) x y

let edge_failed edge =
  match edge with
  | 0~1 -> fail01
  | 1~0 -> fail01
  | 0~2 -> fail02
  | 2~0 -> fail02
  | 1~2 -> fail12
  | 2~1 -> fail12
  | 1~3 -> fail13
  | 3~1 -> fail13
  | _ -> fail23


let trans edge x =
  if edge_failed edge then None
  else mapo (fun x -> x + 1) x

let init node =
  match node with
  | 0n -> Some 0
  | _ -> None

let assert_node node x =
  match x with
  | None -> false
  | _ -> true

let sol = solution {init = init; trans = trans; merge = merge}

assert foldNodes (fun k v acc -> acc && assert_node k v) sol true
